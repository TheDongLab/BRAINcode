#!/bin/awk -f
#############################################
# Author: Xianjun Dong & Zachery Wolfe
# Email: xdong@rics.bwh.harvard.edu
# Version: 0.2
#############################################
# AWK script to convert SAM to BED format for RNAseq pipeline
# XS:A strand used if available; fallback to FLAG
# Default: BED12 format
# Usage: sam2bed -bed12 T -sCol 5 -XSstrand T input.sam > input.bed

BEGIN{
    OFS="\t";
    if(bed12=="" || bed12=="T") bed12="TRUE";
    else bed12="FALSE";
    if(sCol=="") sCol=5;
}

{
    if($0~/^@/) next;  # skip headers
    chr=$3;
    if(and($2,0x4)) next;  # skip unmapped reads

    # Standardize chromosome names for GRCh38
    if(chr~/^[0-9]+$/) chr="chr"chr;
    if(chr~/^MT|Mt|mt$/) chr="chrM";
    if(chr~/^GL|KI|JH|KN|KZ/) chr=chr;  # keep alt contigs as-is

    start12=$4-1;
    end12=start12;
    blockCount=0;
    blockSizes="";
    blockStarts="";

    # Determine strand
    if(XSstrand=="" || XSstrand=="T" || XSstrand=="true") strand=match($0,"XS:A")?substr($0,RSTART+5,1):".";
    if(XSstrand=="F" || XSstrand=="false") strand=(and($2,0x10))?"-":"+";

    # Determine score
    score=$5;
    if(sCol~/^[0-9]+$/) score=$sCol;
    else {  # e.g. NH:i:9
        for(i=1;i<=NF;i++) if(match($i,"^"sCol":")) {score=substr($i,RSTART+RLENGTH+2); break;}
    }
    if(score!~/^[0-9]+$/) {print "Score in BED file must be integers. Exit"; exit;}

    # Read name handling
    name=$1;
    if(name!~/\/[1-2]$/) name=name"/"(and($2,64)?1:2);

    cigar=$6;
    while(match(cigar,"[0-9]+[A-Z]")) {
        m=substr(cigar,RSTART,RLENGTH);
        m1=substr(m,1,length(m)-1);
        m2=substr(m,length(m));
        if(m2~/M|=|X/) {
            blockCount++;
            blockSizes=blockSizes m1",";
            blockStarts=blockStarts (end12-start12)",";
            if(bed12=="FALSE") print chr, end12, end12+m1, name, score, strand;
            end12=end12+m1;
        }
        else if(m2~/N|D/) {
            end12=end12+m1;
        }
        cigar=substr(cigar,RSTART+RLENGTH);
    }

    if(bed12=="TRUE") print chr, start12, end12, name, score, strand, start12, end12, "255,0,0", blockCount, blockSizes, blockStarts;
}
